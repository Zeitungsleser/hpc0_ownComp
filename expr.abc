@ <stdio.hdr>
@ <stdlib.hdr>
@ "expr.hdr"
@ "ustr.hdr"
@ "gen.hdr"

fn exprKindToGenInstr(kind: ExprKind): GenInstr;
fn loadExprAddress(expr: -> Expr): GenReg;

fn createBinaryExpr(kind: ExprKind, left: ->Expr, right: ->Expr): ->Expr
{
    if (kind < EXPR_BINARY || kind > EXPR_BINARY_END){
        assert (0 && "exprKindToGenInstr: no binary Expression");
    }
    local n: ->Expr = malloc(sizeof(*n));
    assert(n);
    n->kind = kind;
    n->left = left;
    n->right = right;
    return n;
}

fn createUnaryExpr(kind: ExprKind, child: ->Expr): -> Expr
{
    if (kind < EXPR_UNARY || kind > EXPR_UNARY_END){
        assert (0 && "exprKindToGenInstr: no unary Expression");
    }
    local n: -> Expr = malloc(sizeof(*n));
    assert(n);
    n->kind = kind;
    n->child = child;
    return n;
}

fn createIntegerExpr(val: -> UStr): ->Expr
{
    local n: ->Expr = malloc(sizeof(*n));
    assert(n);
    n->kind = EXPR_INTEGER;
    n->decimal = val;
    return n;
}

fn createIdentifierExpr(name: -> UStr): ->Expr
{
    local n: -> Expr = malloc(sizeof(*n));
    assert(n);
    n->kind = EXPR_IDENTIFIER;
    n->identifier = name;
    return n;
}

fn releaseExpr(expr: ->Expr)
{
    if (expr->kind == EXPR_INTEGER) {
        free(expr);
    }else if (expr->kind > EXPR_BINARY && expr->kind < EXPR_BINARY_END) {
        releaseExpr(expr->left);
        releaseExpr(expr->right);
        free(expr);
    }
}

fn printExprTree(expr: -> Expr)
{
    if (expr->kind > EXPR_PRIMARY && expr->kind < EXPR_PRIMARY_END) {
        switch (expr->kind) {
            case EXPR_INTEGER: printf(" [ %s ]", expr->decimal); break;
            case EXPR_IDENTIFIER: printf(" [ %s ]", expr->identifier); break;
            default: assert(0);
        }
    }else if (expr->kind > EXPR_BINARY && expr->kind < EXPR_BINARY_END) {
        printf(" [ ");
        switch (expr->kind) {
            case EXPR_ADD: printf("+"); break;
            case EXPR_DIV: printf("/"); break;
            case EXPR_MUL: printf("*"); break;
            case EXPR_SUB: printf("-"); break;
            case EXPR_ASSIGN: printf("{=}"); break;
            default: assert(0);
        }
        printExprTree(expr->left);
        printExprTree(expr->right);
        printf(" ] ");
    }else if (expr->kind > EXPR_UNARY && expr->kind < EXPR_UNARY_END){
        printf(" [ ");
        switch (expr->kind) {
            case EXPR_ADDR: printf("\\&"); break;
            case EXPR_DEREF: printf("*"); break;
            default: assert(0);
        }
        printExprTree(expr->child);
        printf(" ] ");
    }
}

fn printExprLatexTree(expr: -> Expr)
{
    printf("\\item\n\\[\n\\begin{forest}\nfor tree={draw,circle,calign=fixed edge angles}\n");
    printExprTree(expr);
    printf("\n\\end{forest}\n\\]\n");
}

fn loadExpr(expr: -> Expr): GenReg
{
    if (expr->kind > EXPR_PRIMARY && expr->kind < EXPR_PRIMARY_END) {
        switch (expr->kind){
            case EXPR_INTEGER: return genConstant(expr->decimal);
            case EXPR_IDENTIFIER:{
                local addr: GenReg = genLoadAddress(expr->identifier);
                genReleaseReg(addr);
                return genFetch(addr);
            }
            default:
                assert(0 && "loadExpr: no primary expression");
                return 0;
        }
    }else if (expr->kind > EXPR_BINARY && expr->kind < EXPR_BINARY_END) {
        if(expr->kind == EXPR_ASSIGN){
            local val: GenReg = loadExpr(expr->right);
            local addr: GenReg = loadExprAddress(expr->left);
            genReleaseReg(addr);
            genStore(val, addr);
            return val;
        }
        local left: GenReg = loadExpr(expr->left);
        local right: GenReg = loadExpr(expr->right);
        genReleaseReg(left);
        genReleaseReg(right);
        return genInstr(exprKindToGenInstr(expr->kind), left, right);
    }else if (expr->kind > EXPR_UNARY && expr->kind < EXPR_UNARY_END){
        switch(expr->kind){
            case EXPR_ADDR: return loadExprAddress(expr->child);
            case EXPR_DEREF:{
                local addr: GenReg = loadExpr(expr->child);
                genReleaseReg(addr);
                return genFetch(addr);
            }
            default: assert(0); return 0;
        }
    }
    assert(0 && "loadExpr: not all cases handled!");
    return genConstant("0");
    
}

fn exprKindToGenInstr(kind: ExprKind): GenInstr
{
    switch(kind){
        case EXPR_ADD: return GEN_ADD;
        case EXPR_SUB: return GEN_SUB;
        case EXPR_MUL: return GEN_MUL;
        case EXPR_DIV: return GEN_DIV;
        default:
            assert (0 && "exprKindToGenInstr: no binary Expression");
            return 0;
    }
}

fn loadExprAddress(expr: -> Expr): GenReg
{
    switch(expr->kind){
        case EXPR_IDENTIFIER: return genLoadAddress(expr->identifier);
        case EXPR_DEREF:      return loadExpr(expr->child);
        default:              
            assert(0 && "loadExprAddress: Expr has no Address");
            return 0;
    }
}

fn addressableExpr(expr: -> Expr): bool
{
    if(expr->kind == EXPR_IDENTIFIER || expr->kind == EXPR_DEREF){
        return true;
    }
    return false;
}
